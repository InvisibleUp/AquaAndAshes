; Update parallax background timers
; Run once per increment of ScrollX
; TODO: This is awful
; TODO: Update VSCode plugin to do something with below
; @arg[c]: Amount to scroll by (signed)
; @clobbers: a, b, h, l
updateFieldX:
	ld a, c
	add a, a ; sign bit of a into carry
	sbc a, a ; a = 0 if carry == 0, $FF otherwise
	ld b, a  ; BC = sign extended C

	ldh a, [FieldX]
	ld l, a
	ldh a, [FieldX+1]
	ld h, a

	add hl, bc

	ld a, h
	ldh [FieldX+1], a
	ld a, l
	ldh [FieldX], a

updateParallax:
	ld b, a
.Layer1:
	sra a
	sra a
	sra a
	ld [ParallaxLayer1], a
	
.Layer2:
	ld a, b
	sra a
	sra a
	ld [ParallaxLayer2], a

.Layer3:
	ld a, b
	sra a
	ld [ParallaxLayer3], a
.return:
	ret
