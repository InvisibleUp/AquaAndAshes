; Update parallax background timers
; Run once per increment of ScrollX
incFieldX:
	ld hl, FieldX
	ld a, [hl+]
	ld d, a
	ld e, [hl]
	dec de
	ld a, e
	ld [hl-], a
	ld a, d
	ld [hl], a

	ld a, [GlobalTimer]
	ld b, a
	ld hl, ParallaxLayer1

.Layer1:
	; Top updates every 4 frames
	and a, %11
	jr nz, .Layer2
	inc [hl]
.Layer2:
	inc hl
	ld b, a
	call Modulo3
	or a
	jr nz, .Layer3
	inc [hl]
.Layer3:
	inc hl
	rra
	jr c, .return
	inc [hl]
.return:
	ret

; Update parallax background timers
; Run once per decrement of ScrollX
decFieldX:
	ld hl, FieldX
	ld a, [hl+]
	ld d, a
	ld e, [hl]
	inc de
	ld a, e
	ld [hl-], a
	ld [hl], d

	ld a, [GlobalTimer]
	ld b, a
	ld hl, ParallaxLayer1

.Layer1:
	; Top updates every 4 frames
	and a, %11
	jr nz, .Layer2
	dec [hl]
.Layer2:
	inc hl
	ld b, a
	call Modulo3
	or a
	jr nz, .Layer3
	dec [hl]
.Layer3:
	inc hl
	rra
	jr c, .return
	dec [hl]
.return:
	ret	

; LCD STAT interupt routine
scrollLayer:
	; Set next HBLANK interrupt
	ld hl, StatNextLineOff

	; Load table offset into DE
	ld a, [hl+] 
	ld d, a
	ld e, [hl] ; HL points to StatXOff
	inc hl

	; Load value
	ld a, [de]
	ldh [GB_SCANLINE_TRIGGER], a

	; Store incremented pointer
	inc de
	ld a, d
	ld [StatNextLineOff], a
	ld a, e
	ld [StatNextLineOff+1], a

	; Set scroll position
	ld a, [hl+] 
	ld d, a
	ld e, [hl]

	ld a, [de]
	ldh [GB_SCROLL_X], a

	inc de
	ld a, d
	ld [StatXOff], a
	ld a, e
	ld [StatXOff+1], a

	pop hl
	pop af
	reti

; Call this after every VBlank
resetScrollLayer:
	; Reset scroll register
;	ld hl, ParallaxLayer1
;	ld a, [hl]
	xor a
	ldh [GB_SCROLL_X], a

initScrollLayer:

	; Next line table
	ld hl, scrolltables_standard_lines

	; Reset LYC register
	ld a, [hl]
	ldh [GB_SCANLINE_TRIGGER], a

	; Increment LYC pointer to next entry
	inc hl
	ld a, h
	ld [StatNextLineOff], a
	ld a, l
	ld [StatNextLineOff+1], a

	; X offsets
	ld hl, ParallaxLayer1
	ld a, h
	ld [StatXOff], a
	ld a, l
	ld [StatXOff+1], a

ret