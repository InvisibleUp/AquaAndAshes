; Update parallax background timers
; Run once per increment of ScrollX
; TODO: This is awful
; TODO: Update VSCode plugin to do something with below
; @arg[c]: Amount to scroll by (signed)
; @clobbers: a, b, h, l
updateFieldX:
	ld a, c
	add a, a ; sign bit of a into carry
	sbc a, a ; a = 0 if carry == 0, $FF otherwise
	ld b, a  ; BC = sign extended C

	ldh a, [FieldX]
	ld l, a
	ldh a, [FieldX+1]
	ld h, a

	add hl, bc

	ld a, h
	ldh [FieldX+1], a
	ld a, l
	ldh [FieldX], a

updateParallax:
	ld b, a    ; Store low byte of FieldX somewhere
.Layer1:
	and a, %111
	jr nz, .Layer2

	ld a, [ParallaxLayer1]
	add a, c
	ld [ParallaxLayer1], a
	
.Layer2:
	ld a, b
	and a, %11
	jr nz, .Layer3

	ld a, [ParallaxLayer2]
	add a, c
	ld [ParallaxLayer2], a

.Layer3:
	ld a, b
	rra ; Low bit of FieldX is whether or not we should scroll
	ret nc

	ld a, [ParallaxLayer3]
	add a, c
	ld [ParallaxLayer3], a
.return:
	ret
