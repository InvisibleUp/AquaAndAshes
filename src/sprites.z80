SECTION "SPRITES",ROM0
; hl = object RAM pointer
; uses A register as scratch
; uses D register for temp storage
; uses E register for temp storage
GetScreenX:
	push de
	push hl
	; Load FieldX into hl
	ld hl, FieldX
	ld a, [hl+]
	ld h, [hl]
	ld l, a

	; Divide by 2 so that it lines up with path scrolling
	srl h ; Store bit 0 into carry
	rr  l ; Load bit 7 from carry

	; Load ObjX into de from stack
	pop bc
	push bc
	ld a, [bc]
	inc bc
	ld d, a
	ld a, [bc]
	inc bc
	ld e, a

	; ObjX - FieldX (as -FieldX + ObjX)
	NegateHL
	add hl, de

	; Get low byte and exit
	ld a, l
	pop hl
	pop de
ret

; Replaces prepare and the various render routines
; HiRAM has a lot of variables, but no inputs
; de = output sprite location [input]
; hl = input object location [input]
PrepareObject:
	define(`CHAROFFSET', _get(`charStruct', `start'))
	; input
	RegisterPair(CHAR, h, l)
	RegisterPair(OAMBUFF, d, e)

	; Stash OAM buffer so we have some more registers
	push de ; + 2
	InvalidatePair(OAMBUFF)

	; Load attributes into HiRAM
	ld a, CHAR_ref
	ldh [SpriteAttribs], a

	; Load Y into HiRAM
	SeekStructAndSet(`CHAROFFSET', _get(`charStruct', `y'), CHAR_hi, CHAR_lo)
	ld a, CHAR_ref
	ldh [SpriteY], a

	; Calculate X and load into HiRAM
	SeekStructAndSet(`CHAROFFSET', _get(`charStruct', `x'), CHAR_hi, CHAR_lo)
	call GetScreenX
	add a, CHAR_ref
	ldh [SpriteX], a

	; Get animation frame and load into HiRAM 
	; (going a bit out of order to save a register)
	SeekStructAndSet(`CHAROFFSET', _get(`charStruct', `animFrame'), CHAR_hi, CHAR_lo)
	ld a, CHAR_ref ; @ charAnim
	sla a ; Multiply by 2
	ldh [SpriteFrame], a

	; Get the animation number
	SeekStructAndSet(`CHAROFFSET', _get(`charStruct', `anim'), CHAR_hi, CHAR_lo)
	ld a, CHAR_ref ; @ charAnim
	sla a

	; Set HL to next object and stash HL (it's now free!)
	SeekStructAndSet(`CHAROFFSET', _get(`charStruct', `next'), CHAR_hi, CHAR_lo)
	push CHAR_reg ; + 4
	InvalidatePair(CHAR)

	; Look up the animation offset (stored in A)
	RegisterPair(ANIMOFF, d, e)

	ld ANIMOFF_hi, HIGH(animations_pointers_start)
	add a, LOW(animations_pointers_start)
	jr nc, .nocarry_1
	inc ANIMOFF_hi
.nocarry_1
	ld ANIMOFF_lo, a

	; Get pointer to animation table into bc
	; de is now free
	RegisterPair(ANIMTABLE, b, c)
	ld a, ANIMOFF_ref
	ld ANIMTABLE_lo, a
	inc ANIMOFF_reg
	ld a, ANIMOFF_ref
	ld ANIMTABLE_hi, a
	InvalidatePair(ANIMOFF)

	; Get number of sprites and number of frames
	ld a, ANIMTABLE_ref
	inc ANIMTABLE_reg
	inc a ; For loop later
	ldh [SpriteCount], a ; Stash

	ld a, ANIMTABLE_ref
	inc ANIMTABLE_reg
	sla a
	ldh [SpriteSize], a

	inc ANIMTABLE_reg ; Skip loop point

	; Determine if we need to skip to right-facing mappings
	ld a, [SpriteAttribs]
	and a, %10000
	jr nz, .skipRightAdjust

	ld a, ANIMTABLE_ref
	sla a
	add a, ANIMTABLE_lo
	ld ANIMTABLE_lo, a
	jr nc, .nocarry_2
	inc ANIMTABLE_hi
.nocarry_2:
.skipRightAdjust:
	inc ANIMTABLE_reg ; Get to start of mapping list
	
	; Add SpriteFrame * 2 to mapping list
	ld h, 0
	ldh a, [SpriteFrame]
	ld l, a
	add hl, ANIMTABLE_reg
	ld ANIMTABLE_hi, h
	ld ANIMTABLE_lo, l

	RegisterPair(OAMBUFF, h, l)
	ld OAMBUFF_reg, sp+2 ; HL = OAM buffer
	ld a, OAMBUFF_ref
	inc OAMBUFF_reg
	ld d, a
	ld OAMBUFF_hi, OAMBUFF_ref
	ld OAMBUFF_lo, d
.loop:
	; Out of sprites?
	ldh a, [SpriteCount]
	dec a
	jr z, .endloop
	ldh [SpriteCount], a

	; Don't need original mapping list right now, do need registers ( + 6 )
	; I have the suspicision we can get rid of this push/pop
	push ANIMTABLE_reg 

	MovePair(ANIMTABLE, d, e)
	RegisterPair(MAPOFF, b, c)
	; Read mapping data for this frame
	ld a, [ANIMTABLE_reg]
	ld MAPOFF_lo, a
	inc ANIMTABLE_reg
	ld a, [ANIMTABLE_reg]
	ld MAPOFF_hi, a
	; de free, bc = mapping offset
	InvalidatePair(ANIMTABLE)

	RegisterSingle(LOOPCOUNT, d)
	; Get number of sub-sprites
	ld a, [MAPOFF_reg]
	ld LOOPCOUNT_reg, a ; D is loop counter
	inc bc

.loop_subsprite:
	; Okay now we can finally write our output to OAM
	; Using E as temp

	ld a, MAPOFF_ref ; Y (adjust with stashed)
	ld e, a
	ldh a, [SpriteY]
	add a, e
	ld OAMBUFF_ref, a
	inc OAMBUFF_lo
	inc MAPOFF_reg

	ld a, MAPOFF_ref ; X (adjust with stashed)
	ld e, a
	ldh a, [SpriteX]
	add a, e
	ld OAMBUFF_ref, a
	inc OAMBUFF_lo
	inc MAPOFF_reg

	ld a, MAPOFF_ref ; Tile
	ld OAMBUFF_ref, a
	inc OAMBUFF_lo
	inc MAPOFF_reg

	ld a, MAPOFF_ref ; Metadata
	ld OAMBUFF_ref, a
	inc OAMBUFF_lo
	inc MAPOFF_reg

	dec LOOPCOUNT_reg
	jr nz, .loop_subsprite
.endloop_subsprite:

	; And now get the next sprite
	InvalidatePair(MAPOFF)
	RegisterPair(ANIMTABLE, b, c)
	pop ANIMTABLE_reg ; ( + 4 )
	ldh a, [SpriteSize]
	add a, ANIMTABLE_lo
	ld ANIMTABLE_lo, a
	jr nc, .loop
	inc ANIMTABLE_hi
	jr .loop

.endloop:
	InvalidateSingle(LOOPCOUNT)
	MovePair(OAMBUFF, d, e)
	RegisterPair(CHAR, h, l)
	pop CHAR_reg ; Object input ( + 2 )

	pop bc ; Discard OAM output ( + 0 )

	InvalidatePair(ANIMTABLE)
	InvalidatePair(CHAR)
	InvalidatePair(OAMBUFF)
ret

; Clobbers literally everything
; de = output sprite location
; hl = input object location
PrepareAllObjects:
	ld d, HIGH(SpriteTable)
	ld e, LOW(SpriteTable)
	ld hl, charArray
	
	; We're going to need to keep bc on the stack...
	ldh a, [ActiveObjectsCount]
	ld b, a
	ldh a, [ActiveObjectsBitfield]
	ld c, a
.loop:
	rl c
	jr nc, .checkdone
	dec c

	push bc
	call PrepareObject
	pop bc

.checkdone
	ret z
	jr .loop

; Don't call directly!
SpriteDMA:
	ld a, SpriteTable / $100
	ldh [$FF46], a ;start DMA transfer (starts right after instruction)
	ld  a, $28     ;delay...
.wait:                 ;total 5x40 cycles, approx 200ms
	dec a          ;1 cycle
	jr  nz, .wait  ;4 cycles
	ret
SpriteDMA_End:
