include "src/constants.z80"
include "src/variables.z80"
include "src/hardware.inc" ; GB constants
include "src/header.z80" ; ROM header

; Core Libraries
include "src/memory.z80"
include "src/display.z80"
include "src/joypad.z80"
include "src/math.z80"

; Game stuff
include "src/gfxinclude.z80"
include "src/sprites.z80"
include "src/background.z80"
include "src/objects.z80"

SECTION "main", ROM0
; Interrupt handlers
vblank:
    ; Jumping here clears vblank flag automatically
    push af
    ld a, [WaitingForVBlank]
    jr nz, .return ; Lag!

    call $FF80 ; Update sprites
    
    ; Finish
    ld a, 1
    ld [WaitingForVBlank], a

.return:
    pop af
reti

lcdc:
    push af
    push hl

    ld hl, StatJmpLoc
    ld a, [hl+]
    ld l, [hl]
    ld h, a
    jp hl ; This better be valid...

clearRAM:
   ; Clear RAM and VRAM
    xor a

    ld hl, WramStart
    ld bc, WramEnd - WramStart
    call memset

    ld hl, HramStart
    ld bc, HramEnd - HramStart
    call memset

    ; VRAM tiles
    ld hl, $8000
    ld bc, $9FFF - $8000
    call memset

    ; VRAM sprites
    ld hl, $FE00
    ld bc, $FE9F - $FE00
    call memset

    ret

LoadGFX:
    call DisableLCD

    ; Set palette
    ld a, %11100100
    ld [rBGP], a
    ld [rOBP0], a
    ; Light palette
    rlca
    rlca
    ld [rOBP1], a

    ; Set scroll position
    xor a
    ld [rSCX], a
    ld [rSCY], a

    ; Clear RAM
    call clearRAM

    ; Copy DMA code to WRAM
    ld hl, SpriteDMA
    ld de, $FF80
    ld bc, SpriteDMA_End - SpriteDMA
    call memcpy

    ; Load graphics
    ld hl, gfx_start
    ld de, _VRAM
    ld bc, gfx_end - gfx_start
    call memcpy

    ld hl, font
    ld de, _VRAM + $800
    ld bc, font_end - font
    call memcpy

    ld hl, bg_start
    ld de, _VRAM + $1000
    ld bc, bg_end - bg_start
    call memcpy

    ; Load background
    ld de, bgmap_start
    ld hl, _SCRN0
    ld b, 18
.bgLoop_Row:
    ld c, 32
.bgLoop_Column:
    ld a, [de]
    ld [hl+], a
    dec c
    jr nz, .bgLoop_Column

    inc de
    dec b
    jr nz, .bgLoop_Row

    ; Lay down some text
;    ld hl, _SCRN0 + 32
;    ld a, $85
;    ld [hl+], a
;    ld [hl+], a
;    ld [hl+], a
;    ld [hl+], a

    call InitDisplay
ret

; Main Loop
main:
    ; Set stack to top of WRAM
    ; (yes, this odd address kills me inside)
    ld sp, $DFFF

    ; Set interrupts and timers
    xor a
    ldh [rIF], a
    ld a, IEF_VBLANK | IEF_LCDC
    ldh [rIE], a

    ; Init PPU
    call LoadGFX
    
    ; Init objects
    ld hl, SampleObject
    ld de, charStruct
    ld bc, charSizeOf
    call memcpy

    ; Copy DMA code to WRAM
    ld hl, SpriteDMA
    ld de, $FF80
    ld bc, SpriteDMA_End - SpriteDMA
    call memcpy

    ; Testing stuff
    ld a, $C0
    ld [TurnInfo], a

    ; Set parallax interrupt
    ld hl, scrollLayer
	ld a, h
	ld [StatJmpLoc], a
	ld a, l
	ld [StatJmpLoc+1], a

    ; Set parallax data pointers
    call initScrollLayer

    ; Turn on parallax stuff now that it's safe-ish
    ld a, STATF_LYC
    ldh [rSTAT], a 

    WaitForVBlank
    ei

.loop:
    ; Get frame data ready
    call resetScrollLayer
    call GenerateActiveObjectsBitfield 
    call readJoypad

    ; Game logic
    call TickAllObjects

    ; Prepare graphics data
    ;call MoveFieldX
    call incFieldX
    call PrepareAllObjects

    ; Tick global timer
    ld hl, GlobalTimer
    inc [hl]

    ; Wait for VBlank
    xor a
    ld [WaitingForVBlank], a
.VBlankWait
    halt
    ld a, [WaitingForVBlank]
    or a
    jr z, .VBlankWait

    jr .loop
; End Main